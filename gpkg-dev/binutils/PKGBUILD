# The original PKGBUILD was taken from the gitlab.archlinux.org/archlinux/packaging repo:
# - https://gitlab.archlinux.org/archlinux/packaging/packages/binutils/-/blob/main/PKGBUILD

pkgname=binutils-glibc
pkgver=2.41
pkgrel=0
pkgdesc='A set of programs to assemble and manipulate binary and object files'
arch=(any)
url='https://www.gnu.org/software/binutils/'
license=('GPL3' 'GPL' 'FDL1.3' 'custom:FSFAP')
source=(https://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.xz
        gold-warn-unsupported.patch)
sha256sums=('ae9a5789e23459e59606e6714723f2d3ffc31c03174191ef0d015bdf06007450'
            '2d430b66f84a19c154725ec535280c493be1d34f1a90f95208dce02fecd0e4e4')
groups=('gpkg-dev')

prepare() {
  [[ ! -d binutils-gdb ]] && ln -s binutils-$pkgver binutils-gdb
  mkdir -p binutils-build

  cd binutils-gdb

  # Turn off development mode (-Werror, gas run-time checks, date in sonames)
  sed -i '/^development=/s/true/false/' bfd/development.sh

  # Creds @Fedora
  # Change the gold configuration script to only warn about
  # unsupported targets.  This allows the binutils to be built with
  # BPF support enabled.
  patch -Np1 -i "${srcdir}"/gold-warn-unsupported.patch
}

build() {
  cd binutils-build

  CFLAGS+=" -D_REENTRANT"
  LDFLAGS+=" -lcurl -lelf -lssh2 -lssl -lcrypto -lbrotlidec -lbrotlicommon"
  "$srcdir/binutils-gdb/configure" \
    --host=$GPKG_DEV_TARGET \
    --build=$GPKG_DEV_TARGET \
    --target=$GPKG_DEV_TARGET \
    --prefix=$GLIBC_PREFIX \
    --with-lib-path=$GLIBC_PREFIX/lib:$GLIBC_PREFIX/local/lib \
    --with-bugurl=https://github.com/termux-pacman/glibc-packages/issues \
    --enable-cet \
    --enable-deterministic-archives \
    --enable-gold \
    --enable-install-libiberty \
    --enable-ld=default \
    --enable-lto \
    --enable-pgo-build=lto \
    --enable-plugins \
    --enable-relro \
    --enable-shared \
    --enable-threads \
    --disable-gdb \
    --disable-gdbserver \
    --disable-libdecnumber \
    --disable-readline \
    --disable-sim \
    --disable-werror \
    --with-debuginfod \
    --with-pic \
    --with-system-zlib

  make -O tooldir=$GLIBC_PREFIX

  # Extract the FSF All Permissive License
  # <https://www.gnu.org/prep/maintain/html_node/License-Notices-for-Other-Files.html>
  # used for some linker scripts.
  tail -n 5 "${srcdir}"/binutils-${pkgver}/ld/scripttempl/README > FSFAP
}

package() {
  cd binutils-build

  make prefix="$pkgdir/$GLIBC_PREFIX" tooldir="$pkgdir/$GLIBC_PREFIX" install

  # install PIC version of libiberty
  install -m644 libiberty/pic/libiberty.a "${pkgdir}/$GLIBC_PREFIX/lib"

  # Remove unwanted files
  rm -f "$pkgdir"/$GLIBC_PREFIX/share/man/man1/{dlltool,windres,windmc}*

  # No shared linking to these files outside binutils
  rm -f "$pkgdir"/$GLIBC_PREFIX/lib/lib{bfd,opcodes}.so
  echo 'INPUT( $GLIBC_PREFIX/lib/libbfd.a -lsframe -liberty -lz -lzstd -ldl )' > "$pkgdir/$GLIBC_PREFIX/lib/libbfd.so"
  echo 'INPUT( $GLIBC_PREFIX/lib/libopcodes.a -lbfd )' > "$pkgdir/$GLIBC_PREFIX/lib/libopcodes.so"

  # install FSF All Permissive License
  install -Dm644 -t "${pkgdir}"/$GLIBC_PREFIX/share/licenses/${pkgname}/ FSFAP

  rm -f "${pkgdir}"/$GLIBC_PREFIX/share/info/dir

  replace_hard_with_symbolic "$pkgdir"
}
