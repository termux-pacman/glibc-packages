# The original PKGBUILD was taken from the gitlab.archlinux.org/archlinux/packaging repo:
# - https://gitlab.archlinux.org/archlinux/packaging/packages/e2fsprogs/-/blob/main/PKGBUILD

pkgname=e2fsprogs-glibc
pkgver=1.47.0
pkgrel=3
pkgdesc='Ext2/3/4 filesystem utilities'
arch=(any)
license=('GPL' 'LGPL' 'MIT')
url='http://e2fsprogs.sourceforge.net'
source=(https://github.com/tytso/e2fsprogs/archive/refs/tags/v${pkgver}.tar.gz
        'MIT-LICENSE')
sha256sums=('74c8ea97c73294edc6c11dc5e7fbb4324f86c28efd66ad0ba50be4eec8a48be2'
            'cc45386c1d71f438ad648fd7971e49e3074ad9dbacf9dd3a5b4cb61fd294ecbb')
groups=('gpkg-dev')

prepare() {
  cd "${srcdir}/e2fsprogs-${pkgver}"

  # Remove unnecessary init.d directory
  sed -i '/init\.d/s|^|#|' misc/Makefile.in
}

build() {
  cd "${srcdir}/e2fsprogs-${pkgver}"

  export LDFLAGS="${GPKG_DEV_FLAGS}"

  ./configure \
      --prefix=$GLIBC_PREFIX \
      --enable-symlink-install \
      --with-root-prefix='' \
      --libdir=$GLIBC_PREFIX/lib \
      --sbindir=$GLIBC_PREFIX/bin \
      --sysconfdir=$GLIBC_PREFIX/etc \
      --with-systemd-unit-dir=$GLIBC_PREFIX/lib/systemd \
      --with-udev-rules-dir=$GLIBC_PREFIX/lib/udev \
      --enable-elf-shlibs \
      --disable-fsck \
      --disable-uuidd \
      --disable-libuuid \
      --disable-libblkid
  make

  # regenerate locale files
  find po/ -name '*.gmo' -delete
  make -C po update-gmo
}

package() {
  unset MAKEFLAGS

  cd "${srcdir}/e2fsprogs-${pkgver}"

  make DESTDIR="${pkgdir}" install install-libs

  sed -i -e 's/^AWK=.*/AWK=awk/' "${pkgdir}/$GLIBC_PREFIX/bin/compile_et"

  # remove references to build directory
  sed -i -e 's#^DIR=.*#DIR="$GLIBC_PREFIX/share/ss"#' "${pkgdir}/$GLIBC_PREFIX/bin/mk_cmds"
  sed -i -e 's#^DIR=.*#DIR="$GLIBC_PREFIX/share/et"#' "${pkgdir}/$GLIBC_PREFIX/bin/compile_et"

  # remove static libraries with a shared counterpart
  rm "${pkgdir}"/$GLIBC_PREFIX/lib/lib{com_err,e2p,ext2fs,ss}.a

  # install MIT license
  install -D -m0644 "${srcdir}/MIT-LICENSE" \
    "${pkgdir}/$GLIBC_PREFIX/share/licenses/${pkgname}/MIT-LICENSE"
}
