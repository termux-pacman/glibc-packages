# The original PKGBUILD was taken from the archlinux/svntogit-packages repo:
# - https://github.com/archlinux/svntogit-packages/blob/packages/gcc/trunk/PKGBUILD

pkgname=(gcc-glibc gcc-libs)
pkgver=13.1.1
_commit=9a167ee2f8b9a0859fbab6cfdc276cf1f272effe
pkgrel=0
pkgdesc='The GNU Compiler Collection'
arch=(any)
url='https://gcc.gnu.org/'
license=(GPL LGPL FDL)
options=(!emptydirs !strip staticlibs)
#source=(https://ftp.gnu.org/gnu/gcc/gcc-$pkgver/gcc-$pkgver.tar.xz
source=(git+https://sourceware.org/git/gcc.git#commit=${_commit}
        c89 c99
        collect2.cc.patch
        gcc.cc.patch
        set_glibc_dynamic_linker.patch)
_libdir=data/data/com.termux/files/usr/glibc/lib/gcc/$CHOST/${pkgver%%+*}
sha256sums=('SKIP'
            'd87254ee2c816aee66b00dd5002966cf3fe9aca4969eaf160c47150583357790'
            '9ad6d475acf0b19ca0e420f32867c6a5186ffbae50481ec64a140165f8988b60'
            'dcd9760f6fc08b4d441ec60c90e6a63ea0277f8ce4b67a11f103ebdd7a3f498f'
            '8e766ae811e1d769c0e14c78189e52edcc55ced0060288100ae0005174b7dcd2'
            '6164352027df660d0af1f2bdf25d52cc3cfb7f18399d22d48a7cc8429a482e10')
groups=('gpkg-dev')

prepare() {
  for i in *.patch; do
    patch -Np1 -i ${srcdir}/$i
  done

  cd gcc
  sed -i 's|mabi.lp64=../lib64|mabi.lp64=../lib|' gcc/config/aarch64/t-aarch64-linux

  mkdir -p "$srcdir"/gcc-build
}

build() {
  cd gcc-build

  CFLAGS=${CFLAGS/-Werror=format-security/}
  CXXFLAGS=${CXXFLAGS/-Werror=format-security/}

  CONFIGFLAG=""
  case "$CARCH" in
    "aarch64") CONFIGFLAG="--with-arch=armv8-a --enable-fix-cortex-a53-835769 --enable-fix-cortex-a53-843419";;
    "armv7h") CONFIGFLAG="--with-arch=armv7-a --with-float=hard --with-fpu=neon";;
  esac

  "$srcdir"/gcc/configure \
    --prefix=$GLIBC_PREFIX \
    --libdir=$GLIBC_PREFIX/lib \
    --libexecdir=$GLIBC_PREFIX/lib \
    --mandir=$GLIBC_PREFIX/share/man \
    --infodir=$GLIBC_PREFIX/share/info \
    --with-bugurl=https://github.com/termux-pacman/glibc-packages/issues \
    --build=$CHOST \
    $CONFIGFLAG \
    --disable-multilib \
    --disable-bootstrap \
    --disable-nls \
    --enable-default-pie \
    --enable-languages=c,c++ \
    --with-system-zlib \
    --enable-__cxa_atexit \
    --enable-linker-build-id \
    --enable-plugin \
    --with-linker-hash-style=gnu \
    --enable-gnu-indirect-function \
    --disable-werror \
    --disable-checking \
    --disable-static \
    --enable-host-shared \
    --disable-libssp \
    --disable-libstdcxx-pch

  make -O BOOT_CFLAGS="$CFLAGS" \
      BOOT_CXXFLAGS="$CXXFLAGS"
}

package_gcc-libs() {
  pkgdesc='Runtime libraries shipped by GCC'
  options=(!emptydirs !strip)

  cd gcc-build
  make -C $CHOST/libgcc DESTDIR="$pkgdir" install-shared
  rm -f "$pkgdir/$_libdir/libgcc_eh.a"

  for lib in libatomic \
             libgomp \
             libitm \
             libquadmath \
             libsanitizer/{a,l,ub}san \
             libstdc++-v3/src \
             libvtv; do
    make -C $CHOST/$lib DESTDIR="$pkgdir" install-toolexeclibLTLIBRARIES
  done

  if [[ $CARCH == "x86_64" || $CARCH == "aarch64" ]]; then
    make -C $CHOST/libsanitizer/tsan DESTDIR="$pkgdir" install-toolexeclibLTLIBRARIES
  fi

  make -C $CHOST/libstdc++-v3/po DESTDIR="$pkgdir" install

  for lib in libgomp \
             libitm \
             libquadmath; do
    make -C $CHOST/$lib DESTDIR="$pkgdir" install-info
  done

  # Install Runtime Library Exception
  install -Dm644 "$srcdir/gcc/COPYING.RUNTIME" \
    "$pkgdir/$GLIBC_PREFIX/share/licenses/gcc-libs/RUNTIME.LIBRARY.EXCEPTION"

  rm -f "${pkgdir}"/$GLIBC_PREFIX/share/info/dir
}

package_gcc-glibc() {
  pkgdesc="The GNU Compiler Collection - C and C++ frontends"
  options=(!emptydirs)

  cd gcc-build

  make -C gcc DESTDIR="$pkgdir" install-driver install-cpp install-gcc-ar \
    c++.install-common install-headers install-plugin install-lto-wrapper

  install -m755 -t "$pkgdir/$GLIBC_PREFIX/bin/" gcc/gcov{,-tool}
  install -m755 -t "$pkgdir/${_libdir}/" gcc/{cc1,cc1plus,collect2,lto1}

  make -C $CHOST/libgcc DESTDIR="$pkgdir" install
  rm -f "$pkgdir"/$GLIBC_PREFIX/lib/libgcc_s.so*

  make -C $CHOST/libstdc++-v3/src DESTDIR="$pkgdir" install
  make -C $CHOST/libstdc++-v3/include DESTDIR="$pkgdir" install
  make -C $CHOST/libstdc++-v3/libsupc++ DESTDIR="$pkgdir" install
  make -C $CHOST/libstdc++-v3/python DESTDIR="$pkgdir" install

  make DESTDIR="$pkgdir" install-libcc1
  if [ -f "$pkgdir"/$GLIBC_PREFIX/lib/libstdc++.so.6.*-gdb.py ]; then
    install -d "$pkgdir/$GLIBC_PREFIX/share/gdb/auto-load/usr/lib"
    mv "$pkgdir"/$GLIBC_PREFIX/lib/libstdc++.so.6.*-gdb.py \
      "$pkgdir/$GLIBC_PREFIX/share/gdb/auto-load/usr/lib/"
  fi
  rm "$pkgdir"/$GLIBC_PREFIX/lib/libstdc++.so* || true

  make DESTDIR="$pkgdir" install-fixincludes
  make -C gcc DESTDIR="$pkgdir" install-mkheaders

  make -C lto-plugin DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir"/$GLIBC_PREFIX/lib/bfd-plugins/
  ln -s /${_libdir}/liblto_plugin.so \
    "$pkgdir/$GLIBC_PREFIX/lib/bfd-plugins/"

  make -C $CHOST/libgomp DESTDIR="$pkgdir" install-nodist_{libsubinclude,toolexeclib}HEADERS
  make -C $CHOST/libitm DESTDIR="$pkgdir" install-nodist_toolexeclibHEADERS
  make -C $CHOST/libquadmath DESTDIR="$pkgdir" install-nodist_libsubincludeHEADERS
  make -C $CHOST/libsanitizer DESTDIR="$pkgdir" install-nodist_{saninclude,toolexeclib}HEADERS
  make -C $CHOST/libsanitizer/asan DESTDIR="$pkgdir" install-nodist_toolexeclibHEADERS
  if [[ $CARCH == "aarch64" ]]; then
    make -C $CHOST/libsanitizer/tsan DESTDIR="$pkgdir" install-nodist_toolexeclibHEADERS
  fi
  make -C $CHOST/libsanitizer/lsan DESTDIR="$pkgdir" install-nodist_toolexeclibHEADERS

  make -C gcc DESTDIR="$pkgdir" install-man install-info
  rm "$pkgdir"/$GLIBC_PREFIX/share/man/man1/lto-dump.1

  make -C libcpp DESTDIR="$pkgdir" install
  make -C gcc DESTDIR="$pkgdir" install-po

  # many packages expect this symlink
  ln -s gcc "$pkgdir"/$GLIBC_PREFIX/bin/cc

  # POSIX conformance launcher scripts for c89 and c99
  install -Dm755 "$srcdir/c89" "$pkgdir/$GLIBC_PREFIX/bin/c89"
  install -Dm755 "$srcdir/c99" "$pkgdir/$GLIBC_PREFIX/bin/c99"

  # byte-compile python libraries
  python -m compileall "$pkgdir/$GLIBC_PREFIX/share/gcc-${pkgver%%+*}/"
  python -O -m compileall "$pkgdir/$GLIBC_PREFIX/share/gcc-${pkgver%%+*}/"

  # Install Runtime Library Exception
  install -d "$pkgdir/$GLIBC_PREFIX/share/licenses/$pkgname/"
  ln -s $GLIBC_PREFIX/share/licenses/gcc-libs/RUNTIME.LIBRARY.EXCEPTION \
    "$pkgdir/$GLIBC_PREFIX/share/licenses/$pkgname/"

  rm -f "${pkgdir}"/$GLIBC_PREFIX/share/info/dir

  replace_hard_with_symbolic "$pkgdir"
}

