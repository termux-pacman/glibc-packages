# Maintainer: Anatol Pomozov
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Christer Solskogen <christer.solskogen@gmail.com>

pkgname=glibc
pkgver=2.37
pkgrel=0
_commit=be176490b818b65b5162c332eb6b581690b16e5c
pkgdesc="GNU C Library"
arch=(any)
url='https://www.gnu.org/software/libc/'
license=('GPL' 'LGPL')
options=(!strip staticlibs)
source=(https://ftp.gnu.org/gnu/libc/glibc-$pkgver.tar.xz{,.sig}
	'_Fork.c.patch'
	'dl-tls_init_tp.c.patch'
	'pthread_create.c.patch'
        'faccessat.c.patch'
        'tst-rseq-disable.c.patch'
        'tst-rseq.h.patch'
        'rseq-internal.h.patch'
        'disable-clone3.patch'
        'locale-gen'
        'locale.gen.txt'
        'sdt-config.h'
        'sdt.h'
	'grun'
	'statx.c.patch'
	'xstat64.c.patch'
	'lxstat64.c.patch'
	'fxstatat64.c.patch'
	'fxstat64.c.patch'
	'fstatat64.c.patch'
	'clock_gettime.c.patch')
sha256sums=('2257eff111a1815d74f46856daaf40b019c1e553156c69d48ba0cbfc1bb91a43'
            'SKIP'
            '9254552968fb94543cc394ab99c641f2d72198b5f448e1847f7d697e669fbefd'
            'bc0530006920c0038d25bccc2ad63bdf5f2bdfd18374ea7dfa0c59e3e2a16a50'
            '07452446c739a133d440f83589f8e43f7be9727c931447d08eb3b708db1782a8'
            'd201d7bcc91e7c62ca981462280d57326d334c0a7ec9eefa50b8d5ff96341bfd'
            '7b94b8d249685256de0ed6d0fd6d880a9c03234d4879b5f0ac1f52ea0a8042d0'
            '3d39f4166fc13f848eedceb7f877700d36b8613d4b5bdb0c564e7fcf6bf8c95e'
            'd984384ebcb6a4bffd2428cb6ec0dc8c456cd0492eb3dceff80e5e73e37c6e61'
            'a2f394168cdb48a3320195fa056d0540dbb5462e313115247d604e31b24587b1'
            'cdd916c0dc29e5d958ddef5c3530947244fd740ce7645601bfabae53512323b8'
            'd42648cea552ba5353a32e264686e992263289d5cc86207314dffc54ab514981'
            'cdc234959c6fdb43f000d3bb7d1080b0103f4080f5e67bcfe8ae1aaf477812f0'
            '774061aff612a377714a509918a9e0e0aafce708b87d2d7e06b1bd1f6542fe70'
            '016a827f4876283a1f169b2f8849b077450ba5903d8f4c7ff3c2737df4d8ffdc'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')
validpgpkeys=(7273542B39962DF7B299931416792B4EA25340F8  # "Carlos O'Donell <carlos@systemhalted.org>"
              BC7C7372637EC10C57D7AA6579C43DFBF1CF2187) # Siddhesh Poyarekar
groups=('gpkg-dev')

prepare() {
  for i in *.patch; do
    /data/data/com.termux/files/usr/bin/patch -Np1 -i ${srcdir}/$i
  done

  mkdir -p glibc-build

  [[ ! -d glibc ]] && ln -s glibc-$pkgver glibc

  sed -i 's|/tmp|/data/data/com.termux/files/usr/tmp|g' $(grep -R /tmp/ ${srcdir}/glibc-${pkgver} | grep -v /var/ | awk -F'[:=]' '{printf $1 "\n"}')
  sed -i 's|/var/tmp|/data/data/com.termux/files/usr/tmp|g' $(grep -R /var/tmp ${srcdir}/glibc-${pkgver} | awk -F'[:=]' '{printf $1 "\n"}')

  if [ -f ${srcdir}/glibc-${pkgver}/sysdeps/unix/sysv/linux/generic/chmod.c ]; then
    mv ${srcdir}/glibc-${pkgver}/sysdeps/unix/sysv/linux/generic/chmod.c ${srcdir}/glibc-${pkgver}/sysdeps/unix/sysv/linux/chmod.c
  fi
}

build() {
  cd glibc-build
  echo "slibdir=/data/data/com.termux/files/usr/glibc/lib" > configparms
  echo "rtlddir=/data/data/com.termux/files/usr/glibc/lib" >> configparms
  echo "sbindir=/data/data/com.termux/files/usr/glibc/bin" >> configparms
  echo "rootsbindir=/data/data/com.termux/files/usr/glibc/bin" >> configparms

  unset CFLAGS
  ../glibc-$pkgver/configure \
      --prefix=/data/data/com.termux/files/usr/glibc \
      --libdir=/data/data/com.termux/files/usr/glibc/lib \
      --libexecdir=/data/data/com.termux/files/usr/glibc/lib \
      --with-bugurl=https://github.com/Maxython/glibc-for-termux/issues \
      --enable-bind-now \
      --enable-cet \
      --enable-multi-arch \
      --enable-stack-protector=strong \
      --enable-systemtap \
      --disable-profile \
      --disable-crypt \
      --disable-werror CFLAGS="-O2 -pipe"

  echo "build-programs=no" >> configparms
  make -O

  sed -i "/build-programs=/s#no#yes#" configparms
  echo "CFLAGS=-O2 -pipe" >> configparms
  make -O
  make info

  localedef -c -f ../glibc/localedata/charmaps/UTF-8 -i ../glibc/localedata/locales/C ../C.UTF-8/
}

package() {
  backup=(data/data/com.termux/files/usr/glibc/etc/gai.conf
          data/data/com.termux/files/usr/glibc/etc/locale.gen
          data/data/com.termux/files/usr/glibc/etc/nscd.conf)

  make -C glibc-build install_root="$pkgdir" install
  rm -f "$pkgdir"/data/data/com.termux/files/usr/glibc/etc/ld.so.{cache,conf}

  # Shipped in tzdata
  rm -f "$pkgdir"/usr/bin/{tzselect,zdump,zic}

  cd glibc

  install -dm755 "$pkgdir"/data/data/com.termux/files/usr/glibc/lib/{locale,systemd/system,tmpfiles.d}
  install -m644 nscd/nscd.conf "$pkgdir/data/data/com.termux/files/usr/glibc/etc/nscd.conf"
  install -m644 nscd/nscd.service "$pkgdir/data/data/com.termux/files/usr/glibc/lib/systemd/system"
  install -m644 nscd/nscd.tmpfiles "$pkgdir/data/data/com.termux/files/usr/glibc/lib/tmpfiles.d/nscd.conf"
  install -dm755 "$pkgdir/data/data/com.termux/files/usr/glibc/var/db/nscd"

  install -m644 posix/gai.conf "$pkgdir"/data/data/com.termux/files/usr/glibc/etc/gai.conf

  install -m755 "$srcdir/locale-gen" "$pkgdir/data/data/com.termux/files/usr/glibc/bin"

  # Create /etc/locale.gen
  install -m644 "$srcdir/locale.gen.txt" "$pkgdir/data/data/com.termux/files/usr/glibc/etc/locale.gen"
  sed -e '1,3d' -e 's|/| |g' -e 's|\\| |g' -e 's|^|#|g' \
    "$srcdir/glibc/localedata/SUPPORTED" >> "$pkgdir/data/data/com.termux/files/usr/glibc/etc/locale.gen"

  # install C.UTF-8 so that it is always available
  install -dm755 "$pkgdir/data/data/com.termux/files/usr/glibc/lib/locale"
  cp -r "$srcdir/C.UTF-8" -t "$pkgdir/data/data/com.termux/files/usr/glibc/lib/locale"
  sed -i '/#C\.UTF-8 /d' "$pkgdir/data/data/com.termux/files/usr/glibc/etc/locale.gen"

  # Provide tracing probes to libstdc++ for exceptions, possibly for other
  # libraries too. Useful for gdb's catch command.
  install -Dm644 "$srcdir/sdt.h" "$pkgdir/data/data/com.termux/files/usr/glibc/include/sys/sdt.h"
  install -Dm644 "$srcdir/sdt-config.h" "$pkgdir/data/data/com.termux/files/usr/glibc/include/sys/sdt-config.h"

  sed -i 's|!/bin/bash|!/data/data/com.termux/files/usr/bin/bash|' "$pkgdir"/data/data/com.termux/files/usr/glibc/bin/*
  mkdir -p "$pkgdir"/data/data/com.termux/files/usr/bin
  install -m755 "$srcdir/grun" "$pkgdir/data/data/com.termux/files/usr/bin"

  # Replacing hard links with symbolic links
  for i in $(find "$pkgdir" -type l); do
    ln -sf "$(readlink $i)" "$i"
  done
}
