# The original PKGBUILD was taken from the gitlab.archlinux.org/archlinux/packaging repo:
# - https://gitlab.archlinux.org/archlinux/packaging/packages/glibc/-/blob/main/PKGBUILD

pkgname=glibc
pkgver=2.38
pkgrel=2
pkgdesc="GNU C Library"
arch=(any)
url='https://www.gnu.org/software/libc/'
license=('GPL' 'LGPL')
options=(!strip staticlibs)
source=(https://ftp.gnu.org/gnu/libc/glibc-$pkgver.tar.xz
	'_Fork.c.patch'
	'dl-tls_init_tp.c.patch'
	'pthread_create.c.patch'
        'faccessat.c.patch'
        'tst-rseq-disable.c.patch'
        'tst-rseq.h.patch'
        'rseq-internal.h.patch'
        'disable-clone3.patch'
        'locale-gen'
        'locale.gen.txt'
        'sdt-config.h'
        'sdt.h'
	'grun'
	'statx.c.patch'
	'xstat64.c.patch'
	'lxstat64.c.patch'
	'fxstatat64.c.patch'
	'fxstat64.c.patch'
	'fstatat64.c.patch'
	'clock_gettime.c.patch'
        'chmod.c.patch'
        'send.c.patch'
        'setdirs.patch'
        'recv.c.patch'
        'accept.c.patch'
        'shmem-android.h'
        'shmget.c.patch'
        'shmat.c.patch'
        'shmctl.c.patch'
        'shmdt.c.patch'
        'ftruncate.c.patch'
        'aarch64-arch-syscall.h.patch'
        'arm-arch-syscall.h.patch'
        'x86_64-arch-syscall.h.patch'
        'i386-arch-syscall.h.patch'
        'rmdir.c.patch'
        'mprotect.c'
        'Makefile.patch')
sha256sums=('fb82998998b2b29965467bc1b69d152e9c307d2cf301c9eafb4555b770ef3fd2'
            '85153ad597c6101a73a9d1f3f6c35b6b141ddebb99e445ab601ebe95703b35ec'
            '4869ef24cdd59a931c9b5b173d2e3e9eaa67964bb8dcc34e4e6948f3e7856572'
            '94e3766dc6c72aeb535d6cb29ad3309ed39225cebf6cbb0d0b1040c45452e12a'
            '3beb480a0173fe6c4d8643267e5cc7068f1e7ceed1b60f6b28b349a59e93f8b2'
            'ad261ed5bb2278219bc567d698b439a182ae73f42cde440b5ed31cd9088fdbdb'
            'caf6b0f8733d581d7fef9ae01c61b3540ecb3c7d235d9beebafd059270ae0cc8'
            '57ace13145309e73021b94fe4f267b01cc3c03b48b5e07e1772cb38ed8b9173d'
            '29daa97cd75bc932ebb892d5964fbbc29e2fdacb17cf722df3206afec6a5f2de'
            'cdd916c0dc29e5d958ddef5c3530947244fd740ce7645601bfabae53512323b8'
            'd42648cea552ba5353a32e264686e992263289d5cc86207314dffc54ab514981'
            'cdc234959c6fdb43f000d3bb7d1080b0103f4080f5e67bcfe8ae1aaf477812f0'
            '774061aff612a377714a509918a9e0e0aafce708b87d2d7e06b1bd1f6542fe70'
            '7052f21aa16a27081ba1cd426b33560c9c742f0a98238bf251d10aa07ef54d91'
            '9158702571e716618c4e2c7f2b1bf380131bd39320d8e035d8d9842531843538'
            '20a3c3c635419f4cbaf3a5f4eed3282af116ba425a18dcde16a7df669eba9855'
            '210c2824749af41d9ea3f8b934b72256dae4cbe0bdcf4a75a0b61c85f135feca'
            '4823ee80fc64e31cc389380a45c38d414757da90e525b8cc09cd91e37dc7248d'
            '8b70cb99339456e308b6ce07c1948e00a9e0e2dac84a290966afacc16628006a'
            'a639d1fd58a56c56a913f3fba368a528ddaf933662cfc90bd2f598d5d09ff2b8'
            '8556666663d97b0500384204821624f3a7a03231f45ac8c0951d5f8006df779e'
            '9181aa7fe3f31a5f66f83f487119614fd1e61cd962c801e448c1a00d7ebc6afb'
            '0467c9c967793244085cfca3d73324a707316b89087d6d30447c2dd417605a0f'
            '8ed5e685df7c0638299887caed6cf79ce28dcbf0466ae74bec9446946da90021'
            '8ad4caf54ad4c8b528a84e1eaf24feac5cc62bedfc3a056a4b05a840a0f690e6'
            'b8f5bade447e9c5352ff61dc2926de9c5ef7dfd70c879396e22dce42dba1fd56'
            '0298e3d4aca6e7db5d674f318a31cb68257bcba0cac66828b00202e012cbf567'
            '1f2c1b8b79246a862b7c153b548a78ef20e3bfe1d058d3002c45fc36f3af944d'
            '1bebfe9869ec3663213eb299b5357acde0631949644c68c6121b24d5f007afcd'
            '8300d82107ee62b15a06ac3b336707e638f5575041ab81dd43e47a99a976c052'
            'cf962f0fde71d61e55f488356f8ec80791ef6438a00da69dda2a79bbb4fea7ef'
            '42742b49cb15df968965de852ac21a2e9f49c68f0492816a5848f9a15e99fd59'
            'c3bac40fb6156fea3d95b5fdaf7338163cd91789b4a0bf5c113c4eee7c39f971'
            '29707ad92df0273c5e2dd70b12440300e6aff4c37e8530b36f45851937e8b9a5'
            '708665ece353864bb6c32c0e8388c7add57442ee4eecfc22e340e0c9a8b71b5f'
            '92dff2fbcd675a7d90bb5d7ca4761a044d9b3136f683e784eab8ec342809cb58'
            '6c2d79171ad884515d2e1ccdb880eb1c555a44040d288f689381e243a901cb27'
            '8bd6e86fe6651acd0aaa543a5ad36ebd0588827824d330ca3bbba0769f4c8960'
            '735bc5deb54be47eff146bb60d4104bff44c56b150d0009fc655ca18201e5224')
groups=('gpkg-dev')

prepare() {
  for i in *.patch; do
    patch -Np1 -i ${srcdir}/$i
  done

  for i in shmem-android.h mprotect.c; do
    install -Dm644 "${srcdir}/${i}" "glibc-${pkgver}/sysdeps/unix/sysv/linux/${i}"
  done

  rm glibc-$pkgver/sysdeps/unix/sysv/linux/*/clone3.S

  mkdir -p glibc-build || true
}

build() {
  cd glibc-build
  echo "slibdir=$GLIBC_PREFIX/lib" > configparms
  echo "rtlddir=$GLIBC_PREFIX/lib" >> configparms
  echo "sbindir=$GLIBC_PREFIX/bin" >> configparms
  echo "rootsbindir=$GLIBC_PREFIX/bin" >> configparms

  unset CFLAGS
  ../glibc-$pkgver/configure \
      --prefix=$GLIBC_PREFIX \
      --libdir=$GLIBC_PREFIX/lib \
      --libexecdir=$GLIBC_PREFIX/lib \
      --host=$GPKG_DEV_TARGET \
      --build=$GPKG_DEV_TARGET \
      --target=$GPKG_DEV_TARGET \
      --with-bugurl=https://github.com/termux-pacman/glibc-packages/issues \
      --enable-bind-now \
      --enable-cet \
      --disable-multi-arch \
      --enable-stack-protector=strong \
      --enable-systemtap \
      --disable-profile \
      --disable-crypt \
      --disable-werror CFLAGS="-O2 -pipe"

  echo "build-programs=no" >> configparms
  make -O

  sed -i "/build-programs=/s#no#yes#" configparms
  echo "CFLAGS=-O2 -pipe" >> configparms
  make -O
  make info

  localedef -c -f ../glibc-$pkgver/localedata/charmaps/UTF-8 -i ../glibc-$pkgver/localedata/locales/C ../C.UTF-8/
}

package() {
  make -C glibc-build install_root="$pkgdir" install
  rm -f "$pkgdir"/$GLIBC_PREFIX/etc/ld.so.{cache,conf}

  # Shipped in tzdata
  rm -f "$pkgdir"/$GLIBC_PREFIX/bin/{tzselect,zdump,zic}

  cd glibc-$pkgver

  install -dm755 "$pkgdir"/$GLIBC_PREFIX/lib/{locale,systemd/system,tmpfiles.d}
  install -m644 nscd/nscd.conf "$pkgdir/$GLIBC_PREFIX/etc/nscd.conf"
  install -m644 nscd/nscd.service "$pkgdir/$GLIBC_PREFIX/lib/systemd/system"
  install -m644 nscd/nscd.tmpfiles "$pkgdir/$GLIBC_PREFIX/lib/tmpfiles.d/nscd.conf"
  install -dm755 "$pkgdir/$GLIBC_PREFIX/var/db/nscd"

  install -m644 posix/gai.conf "$pkgdir"/$GLIBC_PREFIX/etc/gai.conf

  install -m755 "$srcdir/locale-gen" "$pkgdir/$GLIBC_PREFIX/bin"

  # Create /etc/locale.gen
  install -m644 "$srcdir/locale.gen.txt" "$pkgdir/$GLIBC_PREFIX/etc/locale.gen"
  sed -e '1,3d' -e 's|/| |g' -e 's|\\| |g' -e 's|^|#|g' \
    "$srcdir/glibc-$pkgver/localedata/SUPPORTED" >> "$pkgdir/$GLIBC_PREFIX/etc/locale.gen"

  # install C.UTF-8 so that it is always available
  install -dm755 "$pkgdir/$GLIBC_PREFIX/lib/locale"
  cp -r "$srcdir/C.UTF-8" -t "$pkgdir/$GLIBC_PREFIX/lib/locale"
  sed -i '/#C\.UTF-8 /d' "$pkgdir/$GLIBC_PREFIX/etc/locale.gen"

  # Provide tracing probes to libstdc++ for exceptions, possibly for other
  # libraries too. Useful for gdb's catch command.
  install -Dm644 "$srcdir/sdt.h" "$pkgdir/$GLIBC_PREFIX/include/sys/sdt.h"
  install -Dm644 "$srcdir/sdt-config.h" "$pkgdir/$GLIBC_PREFIX/include/sys/sdt-config.h"

  sed -i "s|!/bin/bash|!$TERMUX_PREFIX/bin/bash|" "$pkgdir"/$GLIBC_PREFIX/bin/*
  mkdir -p "$pkgdir"/$TERMUX_PREFIX/bin
  install -m755 "$srcdir/grun" "$pkgdir/$TERMUX_PREFIX/bin"

  replace_hard_with_symbolic "$pkgdir"
}
