# The original PKGBUILD was taken from the archlinux/svntogit-packages repo:
# - https://github.com/archlinux/svntogit-packages/blob/packages/gmp/trunk/PKGBUILD

pkgname=gmp-glibc
pkgver=6.2.1
pkgrel=0
pkgdesc='A free library for arbitrary precision arithmetic'
arch=(any)
url='https://gmplib.org/'
license=(LGPL3 GPL)
source=(https://gmplib.org/download/gmp/gmp-$pkgver.tar.lz)
md5sums=('03a31d8cbaf29d136252f8f38875ed82')
groups=('gpkg-dev')

build() {
  cd gmp-$pkgver

  ./configure --build=${CHOST} \
    --prefix=/data/data/com.termux/files/usr/glibc \
    --enable-cxx \
    --enable-fat
  make
}

package() {
  cd gmp-$pkgver
  make DESTDIR="${pkgdir}" install
  rm "$pkgdir"/data/data/com.termux/files/usr/glibc/share/info/dir
}

