# The original PKGBUILD was taken from the gitlab.archlinux.org/archlinux/packaging repo:
# - https://gitlab.archlinux.org/archlinux/packaging/packages/keyutils/-/blob/main/PKGBUILD

pkgname=keyutils-glibc
_tag='5678a1aae8834b5c16b5ed7dc72ef8836a29e122' # git rev-parse v${pkgver}
pkgver=1.6.3
pkgrel=0
pkgdesc='Linux Key Management Utilities'
arch=(any)
url='https://www.kernel.org/'
license=('GPL2' 'LGPL2.1')
backup=('data/data/com.termux/files/usr/glibc/etc/request-key.conf')
source=("git+https://git.kernel.org/pub/scm/linux/kernel/git/dhowells/keyutils.git#tag=${_tag}"
        'request-key.conf.patch' 'reproducible.patch' 'Makefile.patch')
sha256sums=('SKIP'
            '1ecf854011ce1a15e238653187fd6aed903b94c16d6a99bfca463bd8ca1c2f40'
            '7bb7400b2b8c8f0288c86ec9191f8964a1e682745a204013d5fc7c2e1a253d8e'
            'b349b8e63db5ed3133a80236b487c453220b35837d7502eb81ec24208b191eb0')
groups=('gpkg-dev')

prepare() {
  cd keyutils
  # fix paths of binaries in /etc/request-key.conf
  patch -Np0 -i ../request-key.conf.patch

  # make keyutils reproducible
  patch -Np1 -i ../reproducible.patch

  # symlink setting
  patch -Np1 -i ../Makefile.patch
}

build() {
  cd keyutils

  make CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}" \
    SBINDIR="$GLIBC_PREFIX/bin" \
    BINDIR="$GLIBC_PREFIX/bin"
}

package() {
  cd keyutils
  mkdir -p "${pkgdir}/$GLIBC_PREFIX"
  make DESTDIR="${pkgdir}/$GLIBC_PREFIX" \
    SBINDIR='/bin' \
    BINDIR='/bin' \
    LIBDIR='/lib' \
    USRLIBDIR="/lib" \
    INCLUDEDIR="/include" \
    SHAREDIR="/share/keyutils" \
    MANDIR="/share/man" install
}
