# The original PKGBUILD was taken from the archlinux/svntogit-packages repo:
# - https://github.com/archlinux/svntogit-packages/blob/packages/krb5/trunk/PKGBUILD

pkgname=krb5-glibc
_pkgvermajor=1.20
pkgver=1.20.1
pkgrel=1
pkgdesc='The Kerberos network authentication system'
url='https://web.mit.edu/kerberos/'
arch=(any)
license=('custom')
makedepends=('perl')
backup=(
  'data/data/com.termux/files/usr/glibc/etc/krb5.conf'
  'data/data/com.termux/files/usr/glibc/var/lib/krb5kdc/kdc.conf'
)
options=('!emptydirs')
source=(https://web.mit.edu/kerberos/dist/krb5/${_pkgvermajor}/krb5-${pkgver}.tar.gz
        krb5-config_LDFLAGS.patch)
sha512sums=('6f57479f13f107cd84f30de5c758eb6b9fc59171329c13e5da6073b806755f8d163eb7bd84767ea861ad6458ea0c9eeb00ee044d3bcad01ef136e9888564b6a2'
            '5a3782ff17b383f8cd0415fd13538ab56afd788130d6ad640e9f2682b7deaae7f25713ce358058ed771091040dccf62a3bc87e6fd473d505ec189a95debcc801')
groups=('gpkg-dev')

prepare() {
  cd krb5-${pkgver}

  # cf https://bugs.gentoo.org/show_bug.cgi?id=448778
  patch -Np1 < "${srcdir}"/krb5-config_LDFLAGS.patch

  # FS#25384
  sed -i "/KRB5ROOT=/s/\/local//" src/util/ac_check_krb5.m4
}

build() {
   cd krb5-${pkgver}/src

   export CFLAGS+=" -fPIC -fno-strict-aliasing -fstack-protector-all"
   export CPPFLAGS+=" -I/usr/include/et"
   ./configure --prefix=$GLIBC_PREFIX \
               --sbindir=$GLIBC_PREFIX/bin \
               --sysconfdir=$GLIBC_PREFIX/etc \
               --localstatedir=$GLIBC_PREFIX/var/lib \
               --enable-shared \
               --with-system-et \
               --with-system-ss \
               --disable-rpath \
               --without-tcl \
               --enable-dns-for-realm \
               --with-ldap \
               --without-system-verto
   make
}

package() {
   cd krb5-${pkgver}/src
   make DESTDIR="${pkgdir}" EXAMPLEDIR=$GLIBC_PREFIX/share/doc/${pkgname}/examples install

   # Fix FS#29889
   install -m 644 plugins/kdb/ldap/libkdb_ldap/kerberos.{ldif,schema} \
     "${pkgdir}/$GLIBC_PREFIX/share/doc/${pkgname}/examples"

   install -Dpm 644 config-files/krb5.conf -t "${pkgdir}/$GLIBC_PREFIX/etc"
   install -Dpm 644 config-files/kdc.conf -t "${pkgdir}/$GLIBC_PREFIX/var/lib/krb5kdc"

   install -Dm 644 util/ac_check_krb5.m4 -t "${pkgdir}/$GLIBC_PREFIX/share/aclocal"

   install -Dm 644 "${srcdir}"/krb5-${pkgver}/NOTICE \
     "${pkgdir}/$GLIBC_PREFIX/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
