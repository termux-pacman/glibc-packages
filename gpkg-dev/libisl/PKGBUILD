# The original PKGBUILD was taken from the archlinux/svntogit-packages repo:
# - https://github.com/archlinux/svntogit-packages/blob/packages/libisl/trunk/PKGBUILD

pkgname=libisl-glibc
pkgver=0.26
pkgrel=0
pkgdesc='Library for manipulating sets and relations of integer points bounded by linear constraints'
arch=('any')
url='https://libisl.sourceforge.io'
license=('MIT')
options=(staticlibs)
source=("https://libisl.sourceforge.io/isl-${pkgver}.tar.xz")
b2sums=('2cfe2e0774a991d2385e66f58490c5454382cabc82d74eb4bbe5a603293b7bdc34ca3b087f0884f9da319c447747edc76275ecaf2f171237939f9c7d3c52ced7')
groups=('gpkg-dev')

build() {
  cd "${srcdir}"/isl-${pkgver}
  ./configure --prefix=/data/data/com.termux/files/usr/glibc
  make
}

package() {
  cd "${srcdir}"/isl-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}"/data/data/com.termux/files/usr/glibc/share/gdb/auto-load/usr/lib/
  mv "${pkgdir}"/data/data/com.termux/files/usr/glibc/lib/libisl.so.*-gdb.py "${pkgdir}"/data/data/com.termux/files/usr/glibc/share/gdb/auto-load/usr/lib/
  
  install -Dm644 LICENSE "${pkgdir}"/data/data/com.termux/files/usr/glibc/share/licenses/isl/LICENSE
}
