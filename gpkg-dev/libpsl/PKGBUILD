# The original PKGBUILD was taken from the gitlab.archlinux.org/archlinux/packaging repo:
# - https://gitlab.archlinux.org/archlinux/packaging/packages/libpsl/-/blob/main/PKGBUILD

pkgname=libpsl-glibc
pkgver=0.21.2
pkgrel=0
pkgdesc='Public Suffix List library'
url='https://github.com/rockdaboot/libpsl'
arch=(any)
license=('MIT')
makedepends=('libxslt' 'python' 'publicsuffix-list' 'gtk-doc')
source=(https://github.com/rockdaboot/libpsl/releases/download/${pkgver}/libpsl-${pkgver}.tar.gz)
sha512sums=('f1df72220bf4391d4701007100b0df66c833a2cbcb7481c9d13f0b9e0cad3b66d2d15d4b976e5bad60d2ad1540355112fa1acb07aa925c241d2d7cd20681c71d')
b2sums=('b50f805bb467dc3284bc91645a37a2208098ad809d3771c74ef13c3b8f6029a07ad80a56702c7e3d1a1125d272403c85dd97b64a28465b9ff2d095eaf94b9a4d')
groups=('gpkg-dev')

prepare() {
  cd libpsl-${pkgver}
  rm -frv list
  autoreconf -fiv
}

build() {
  cd libpsl-${pkgver}

  ./configure \
    --prefix=$GLIBC_PREFIX \
    --disable-static \
    --disable-dependency-tracking \
    --enable-man \
    --enable-gtk-doc \
    --enable-{builtin,runtime}=libidn2 \
    --with-psl-file=$GLIBC_PREFIX/share/publicsuffix/effective_tld_names.dat \
    --with-psl-testfile=$GLIBC_PREFIX/share/publicsuffix/test_psl.txt
  LC_CTYPE=en_US.UTF-8 make
}

package() {
  cd libpsl-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/$GLIBC_PREFIX/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
