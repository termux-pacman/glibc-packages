# The original PKGBUILD was taken from the gitlab.archlinux.org/archlinux/packaging repo:
# - https://gitlab.archlinux.org/archlinux/packaging/packages/openssl-1.1/-/blob/main/PKGBUILD

pkgname=openssl-glibc-1.1
_pkgname=openssl
_ver=1.1.1t
# use a pacman compatible version scheme
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=(any)
url='https://www.openssl.org'
license=('custom:BSD')
source=("https://www.openssl.org/source/${_pkgname}-${_ver}.tar.gz"
        'ca-dir.patch')
sha256sums=('8dee9b24bdb1dcbf0c3d1e9b02fb8f6bf22165e807f45adeb7c9677536859d3b'
            '9ffaf3fb5443fe1b7a18dc407f04a4a781834bcbc52187ff97f4c1fa1e2f8cdf')
groups=('gpkg-dev')

prepare() {
	cd "$srcdir/$_pkgname-$_ver"

	# set ca dir to /etc/ssl by default
	patch -p0 -i "$srcdir/ca-dir.patch"
}

build() {
	cd "$srcdir/$_pkgname-$_ver"

	if [ "${CARCH}" = "armv7h" ]; then
		openssltarget='linux-armv4'
		optflags=''
	elif [ "${CARCH}" = "aarch64" ]; then
		openssltarget='linux-aarch64'
		optflags='no-afalgeng'
	elif [ "${CARCH}" = "x86_64" ]; then
		openssltarget='linux-x86_64'
		optflags='enable-ec_nistp_64_gcc_128'
	elif [ "${CARCH}" = "i686" ]; then
		openssltarget='linux-generic32'
		optflags='no-sse2'
	fi

	# mark stack as non-executable: http://bugs.archlinux.org/task/12434
	./Configure --prefix=$GLIBC_PREFIX --openssldir=$GLIBC_PREFIX/etc/ssl --libdir=lib/openssl-1.1 \
		shared no-ssl3-method ${optflags} "${openssltarget}" \
		"-Wa,--noexecstack ${CPPFLAGS} ${CFLAGS} ${LDFLAGS}"

	make depend
	make
}

package() {
	cd "$srcdir/$_pkgname-$_ver"

	make DESTDIR="$pkgdir" install_sw

	# Move some files around
	install -m755 -d "$pkgdir/$GLIBC_PREFIX/include/openssl-1.1"
	mv "$pkgdir/$GLIBC_PREFIX/include/openssl" "$pkgdir/$GLIBC_PREFIX/include/openssl-1.1/"
	mv "$pkgdir/$GLIBC_PREFIX/lib/openssl-1.1/libcrypto.so.1.1" "$pkgdir/$GLIBC_PREFIX/lib/"
	mv "$pkgdir/$GLIBC_PREFIX/lib/openssl-1.1/libssl.so.1.1" "$pkgdir/$GLIBC_PREFIX/lib/"
	ln -sf ../libssl.so.1.1 "$pkgdir/$GLIBC_PREFIX/lib/openssl-1.1/libssl.so"
	ln -sf ../libcrypto.so.1.1 "$pkgdir/$GLIBC_PREFIX/lib/openssl-1.1/libcrypto.so"
	mv "$pkgdir/$GLIBC_PREFIX/bin/openssl" "$pkgdir/$GLIBC_PREFIX/bin/openssl-1.1"

	# Update includedir in .pc files
	sed -e 's|/include$|/include/openssl-1.1|' -i "$pkgdir"/$GLIBC_PREFIX/lib/openssl-1.1/pkgconfig/*.pc

	rm -rf "$pkgdir"/$GLIBC_PREFIX/{etc,bin/c_rehash}

	install -D -m644 LICENSE "$pkgdir/$GLIBC_PREFIX/share/licenses/$pkgname/LICENSE"
}
