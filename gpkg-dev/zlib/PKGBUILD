# The original PKGBUILD was taken from the archlinux/svntogit-packages repo:
# - https://github.com/archlinux/svntogit-packages/blob/packages/zlib/trunk/PKGBUILD

pkgbase=zlib-glibc
pkgname=(zlib-glibc minizip-glibc)
epoch=1
pkgver=1.2.13
pkgrel=0
arch=(any)
license=('custom')
url="https://www.zlib.net/"
options=('staticlibs')  # needed by binutils testsuite
source=("https://zlib.net/zlib-${pkgver}.tar.gz")
sha256sums=('b3a24de97a8fdbc835b9833169501030b8977031bcb54b3b3ac13740f846ab30')
groups=('gpkg-dev')

prepare() {
	cd "${srcdir}/zlib-$pkgver"
	grep -A 24 '^  Copyright' zlib.h > LICENSE
}

build() {
	cd "${srcdir}/zlib-$pkgver"

	CFLAGS+=" -ffat-lto-objects"
	./configure --prefix=/data/data/com.termux/files/usr/glibc
	make

	cd contrib/minizip
	cp Makefile Makefile.orig
	cp ../README.contrib readme.txt
	autoreconf --install
	./configure --prefix=/data/data/com.termux/files/usr/glibc --enable-static=no
	make
}

package_zlib-glibc() {
	pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'

	cd "${srcdir}/zlib-$pkgver"
	make install DESTDIR="${pkgdir}"
	install -D -m644 LICENSE "${pkgdir}/data/data/com.termux/files/usr/glibc/share/licenses/zlib/LICENSE"
}

package_minizip-glibc() {
	pkgdesc='Mini zip and unzip based on zlib'

	cd "${srcdir}/zlib-$pkgver/contrib/minizip"
	make install DESTDIR="${pkgdir}"
	install -D -m644 "${srcdir}/zlib-$pkgver/LICENSE" "${pkgdir}/data/data/com.termux/files/usr/glibc/share/licenses/minizip/LICENSE"

	# https://github.com/madler/zlib/pull/229
	rm "${pkgdir}/data/data/com.termux/files/usr/glibc/include/minizip/crypt.h"
}

