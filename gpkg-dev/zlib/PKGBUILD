# The original PKGBUILD was taken from the gitlab.archlinux.org/archlinux/packaging repo:
# - https://gitlab.archlinux.org/archlinux/packaging/packages/zlib/-/blob/main/PKGBUILD

pkgbase=zlib-glibc
pkgname=(zlib-glibc minizip-glibc)
epoch=1
pkgver=1.2.13
pkgrel=0
arch=(any)
license=('custom')
url="https://www.zlib.net/"
options=('staticlibs')  # needed by binutils testsuite
source=("https://zlib.net/zlib-${pkgver}.tar.gz")
sha256sums=('b3a24de97a8fdbc835b9833169501030b8977031bcb54b3b3ac13740f846ab30')
groups=('gpkg-dev')

prepare() {
	cd "${srcdir}/zlib-$pkgver"
	grep -A 24 '^  Copyright' zlib.h > LICENSE
}

build() {
	cd "${srcdir}/zlib-$pkgver"

	CFLAGS+=" -ffat-lto-objects"
	./configure --prefix=$GLIBC_PREFIX
	make

	cd contrib/minizip
	cp Makefile Makefile.orig
	cp ../README.contrib readme.txt
	autoreconf --install
	./configure --prefix=$GLIBC_PREFIX --enable-static=no
	make
}

package_zlib-glibc() {
	pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'

	cd "${srcdir}/zlib-$pkgver"
	make install DESTDIR="${pkgdir}"
	install -D -m644 LICENSE "${pkgdir}/$GLIBC_PREFIX/share/licenses/zlib/LICENSE"
}

package_minizip-glibc() {
	pkgdesc='Mini zip and unzip based on zlib'

	cd "${srcdir}/zlib-$pkgver/contrib/minizip"
	make install DESTDIR="${pkgdir}"
	install -D -m644 "${srcdir}/zlib-$pkgver/LICENSE" "${pkgdir}/$GLIBC_PREFIX/share/licenses/minizip/LICENSE"

	# https://github.com/madler/zlib/pull/229
	rm "${pkgdir}/$GLIBC_PREFIX/include/minizip/crypt.h"
}

